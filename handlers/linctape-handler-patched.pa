/
	*230
/
/
TREAD,	0
	JMP	TTREAD		/GO TO THE READ DOITER.
TWRITE,	0
	TAD	TWRITE		/GET THE WRITE ADDRESS
	DCA	TREAD		/AND SOTRE BACK IN THE READ FOR THE ARG GETTER.
	JMS	TDO		/CALL THE DOER
	LMODE
	WRI			/A WRITE INSTRUCTION
	PMODE
TL400,	400			/A NOP IN CASE OF "PHOONEY" ERROR.
	JMS	TDO		/NOW DO CHECK
	LMODE
	CHK
	PMODE
	JMP	TWRITE+3	/AN ERROR. TRY AGAIN
/
TTRET,	CLA CLL IAC RTL		/+4 TO SKIP PAST ARGUEMENTS
	TAD	TREAD		/CALLING ADDRESS
	DCA	TREAD		/RETURN ADDRESS
	JMP I	TREAD		/RETURN TO CALLER
/
TTREAD,	JMS	TDO		/DO THE READ NOW
	LMODE
	RDE
	PMODE
	JMP	TTREAD		/ON AN ERROR, RETRY
	JMP	TTRET		/RETURN IF NO ERROR.
/
/
/
/
/
 TDO,	0
	TAD	TREAD		/GET THE CALLING ADDRESS
	DCA	TCALLY		/SAVE AWAY
	DCA	TOPR		/SAVE IT AWAY
	CLA CMA			/SET FOR OPERATION OK
	DCA	TRET		/RETURN CHECKER
	TAD I	TCALLY		/GET THE UNIT NUMBER
	AND	TL7		/CHOP OFF CRAP.
	CLL RAR			/SET TO RIGHT PLACES IN AXO.
	DCA	TAXO		/SAVE IN THE TAPE REGISTER WORD.
	RTL			/NOW ROTATE SINGLE UNITS BIT
	RTL			/BACK AGAIN TO BIT 8.
	TAD I	TDO		/GET THE OPERATION
	ISZ	TDO		/BOP UP THE RETURN ADDRESS
				/ADD IN THE INSTRUCTION AND
	DCA	TOPR		/SAVE IT AWAY. INSTRUCTION NOW COMPLETE.
	ISZ	TCALLY		/BOP POINTER TO CORE LOCATION
	TAD I	TCALLY		/GET THE CORE LOCATION NOW.
	AND	TL160		/JUST GET THE FIELD BITS NOW.
	CLL RTL			/AND ROTATE TO BITS
	RTL			/0-2
	RAL
	TAD	TAXO		/ADD IN THE TAPE REGISTER WORD.
	TAD	TL20		/ALSO ADD IN THE EXTENDED ADDRESS BIT.
	DCA	TAXO		/AND SAVE IT AWAY.
	TAD I	TCALLY		/GET THE CORE LOCATION AGAIN.
	RTR			/ROTATE TO BITS 0-3.
	RTR
	RAR
	AND	TL7400		/AND AND OUT STRAY CRAP.
	DCA	TWRITE		/AND SAVE AWAY. TWRITE IS FREE NOW.
	ISZ	TCALLY		/BOP TO THE BLOCK NUMBER
	TAD I	TCALLY		/GET THE BLOCK NUMBER
	DCA	TBLOCK		/AND SAVE AWAY.
	ISZ	TCALLY		/BOP TO THE NUMBER OF BLOCKS TO READ IN.
	TAD I	TCALLY		/GET THE NUMBER NOW.
	CIA			/NEGATE.
	SNA			/NO BLOCKS?
	JMP	TOUT		/NO BLOCKS. RETURN.
	DCA	TCOUNT		/SET UP THE COUNT NOW.
/
/
TLOOP,	TAD	TWRITE		/GET THE CORE ADDRESS.
	LINC			/GET INTO LINC MODE
	LMODE
	TMA			/SEND TO THE CONTROLLER.
	LDA I			/GET THE FUNCTIONS.
TAXO,	0			/AND SEND IT TO THE CONTROLLER.
	AXO			/WITH AN AXO.
/
TOPR,	0			/NOW DO THE ACTUAL TAPE INSTRUCTION.
TBLOCK,	0			/WITH THE BLOCK NUMBER HERE. THIS HANGS UNTIL DONE.
/
	PDP			/BACK INTO 8 MODE
	PMODE			/FOR NORMAL CRAP
/
	AND	TRET		/CHECK FOR ALL 7777S
	DCA	TRET
	TAD	TL400		/ADD 400 TO THE LOC
	TAD	TWRITE
	DCA	TWRITE
	ISZ	TBLOCK		/BOP UP THE BLOCK NUMBER
	ISZ	TCOUNT		/ALL DONE???
	JMP	TLOOP		/NOPE. DO SOMMSE MORE...
	JMP	TOUT		/ALL DONE. CHECK CHECKSUM AND EXIT.
/
/
/
TL7,	7
TL20,	20
TL160,	160
TL7400,	7400
/
/
/
	*350
TCALLY,	0			/PLACE OF D.B. LOCATIONS.
TCOUNT,	0
/
/
/
TOUT,	ISZ	TRET		/IS THE CHECKSUM OK????
	JMP I	TDO		/NOPE. EXIT
	ISZ	TDO		/OK.  GO TO SECOND LOC
	JMP I	TDO		/RETURN.
/
/
/
/
/
/
/
TRET,	0
/
/
/
/
*357
TREBS,	CDF 10			/DF TO FIELD 1
	JMS	TREAD		/READ IN IO BLKS
	100			/SYS UNIT
	16			/7000 in field 1
	322			/IO ROUTINES
	2			/TWO BLOCKS
	CIF 0		/TO IF 0
	JMP I .+1		/JMP TO 7777
	7777
$
